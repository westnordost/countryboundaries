plugins {
    id "java-library"
    id "maven-publish"
    id 'signing'
}

version = "1.5"
group = "de.westnordost"

dependencies {
    testImplementation "junit:junit:4.13.2"
}

java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId "countryboundaries"
            from components.java
            pom {
                name = "countryboundaries"
                description = "Java library to enable fast offline reverse country geocoding: " +
                        "Find out the country / state in which a geo position is located."
                url = "https://github.com/westnordost/countryboundaries"
                scm {
                    connection = "https://github.com/westnordost/countryboundaries.git"
                    developerConnection = "https://github.com/westnordost/countryboundaries.gitt"
                    url = "https://github.com/westnordost/countryboundaries"
                }
                licenses {
                    license {
                        name = "GNU Lesser General Public License, Version 3.0"
                        url = "http://www.gnu.org/licenses/lgpl-3.0.html"
                    }
                }
                developers {
                    developer {
                        id = "westnordost"
                        name = "Tobias Zwick"
                        email = "osm@westnordost.de"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "mavenCentral"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
